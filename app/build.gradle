apply plugin: 'com.android.application'

android {
    compileSdk 30

    defaultConfig {
        applicationId 'com.winlator'
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 16
        versionName "7.1"
    }

    buildTypes {
        debug {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            ndk {
                abiFilters 'arm64-v8a'
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }

    ndkVersion '22.1.7171670'

    packagingOptions {
        pickFirst 'lib/arm64-v8a/libFLAC.so'  // resolve duplicate from jniLibs + CMake
    }

    externalNativeBuild {
        cmake {
            path 'src/main/cpp/CMakeLists.txt'
        }
    }
}

// Workaround: MergeNativeLibs sees duplicate libFLAC.so (jniLibs + cxx). Delete cxx copy before merge runs.
afterEvaluate {
    def removeDup = tasks.register('removeDuplicateLibFlacDebug') {
        doLast {
            def dir = file("${buildDir}/intermediates/cxx")
            if (dir.exists()) {
                fileTree(dir) { include '**/obj/arm64-v8a/libFLAC.so' }.each { it.delete() }
            }
        }
    }
    tasks.named('mergeDebugNativeLibs').configure { it.dependsOn(removeDup) }
    tasks.named('externalNativeBuildDebug').configure { removeDup.get().mustRunAfter(it) }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.luben:zstd-jni:1.5.2-3@aar'
    implementation 'org.tukaani:xz:1.7'
    implementation 'org.apache.commons:commons-compress:1.20'
}
